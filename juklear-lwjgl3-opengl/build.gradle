import groovy.json.JsonOutput
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'net.janrupf'

repositories {
    mavenCentral()
}

def lwjglClassifier = {
    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        return "natives-windows"
    } else if(Os.isFamily(Os.FAMILY_MAC)) {
        return "natives-macos"
    } else if(Os.isFamily(Os.FAMILY_UNIX)) {
        return "natives-linux"
    } else {
        throw new UnsupportedOperationException("This OS is not supported")
    }
}

dependencies {
    implementation group: 'org.lwjgl', name: 'lwjgl', version: '3.2.2'
    runtimeOnly group: 'org.lwjgl', name: 'lwjgl', version: '3.2.2', classifier: lwjglClassifier()

    implementation group: 'org.lwjgl', name: 'lwjgl-opengl', version: '3.2.2'
    runtimeOnly group: 'org.lwjgl', name: 'lwjgl-opengl', version: '3.2.2', classifier: lwjglClassifier()

    implementation project(':juklear-base')

    testImplementation group: 'org.lwjgl', name: 'lwjgl-glfw', version: '3.2.2'
    testRuntimeOnly group: 'org.lwjgl', name: 'lwjgl-glfw', version: '3.2.2', classifier: lwjglClassifier()
}

task runTestWithRenderdoc {
    doLast {
        File.createTempFile("temp", ".cap").with {renderdocCaptureFile ->
            renderdocCaptureFile.deleteOnExit()

            renderdocCaptureFile.text = JsonOutput.toJson([
                    rdocCaptureSettings: 1,
                    settings: [
                            autoStart: true,
                            commandLine: "-Djava.library.path=${nativeBinariesDir.absolutePath} " +
                                    "-Dfile.encoding=UTF-8 " +
                                    "-classpath ${sourceSets.test.runtimeClasspath.asPath} " +
                                    "net.janrupf.juklear.lwjgl.opengl.GlfwTest",
                            environment: [],
                            executable: "/usr/bin/java",
                            inject: false,
                            numQueuedFrames: 0,
                            options: [
                                    allowFullscreen: true,
                                    allowVSync: true,
                                    apiValidation: false,
                                    captureAllCmdLists: false,
                                    captureCallstacks: false,
                                    captureCallstacksOnlyDraws: false,
                                    debugOutputMute: true,
                                    delayForDebugger: 0,
                                    hookIntoChildren: false,
                                    refAllResources: false,
                                    verifyBufferAccess: false
                            ],
                            queueFrameCap: 0,
                            workingDir: ""
                    ]
            ])

            exec {
                commandLine 'qrenderdoc', renderdocCaptureFile.absolutePath
            }
        }
    }
}

commonPublish(project) {
    pom {
        name = "Juklear LJWGL3 OpenGL"
        description = "plug-and-play Juklear rendering for OpenGL using LWJGL3"

        withXml {
            def node = asNode()
            node.dependencies.'*'.findAll {
                it.groupId.text() == 'org.lwjgl' && it.classifier.text() == lwjglClassifier()
            }.each {
                it.parent().remove(it)
            }

            node.dependencies.'*'.findAll {
                it.groupId.text() == 'net.janrupf' && it.artifactId.text() == 'juklear-base'
            }.each {
                it.scope*.value = 'compile'
            }
        }
    }
}
